#   Copyright (C) 1997, 2002, 2003 Free Software Foundation, Inc.

# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.  

# GCC testsuite for precompiled header interaction,
# that uses the `dg.exp' driver.

# Load support procs.
load_lib gcc-dg.exp

# Initialize `dg'.
dg-init

set old_dg_do_what_default "${dg-do-what-default}"

# Main loop.
foreach test [lsort [glob -nocomplain $srcdir/$subdir/*.c]] {
    global runtests torture_without_loops dg-do-what-default

    # If we're only testing specific files and this isn't one of them, skip it.
    if ![runtest_file_p $runtests $test] {
	continue
    }
    set nshort "$subdir/[file tail $test]"
    set bname "[file rootname [file tail $test]]"

    catch { file delete "$bname.h.gch" }
    catch { file delete "$bname.s" }
    catch { file delete "$bname.s-gch" }

    # We don't try to use the loop-optimizing options, since they are highly
    # unlikely to make any difference to PCH.  However, we do want to
    # add -O0 -g, since users who want PCH usually want debugging and quick
    # compiles.
    set pch_torture [concat [list {-O0 -g}] $torture_without_loops]
    foreach flags $pch_torture {
	verbose "Testing $nshort, $flags" 1

	# For the header files, the default is to precompile.
	set dg-do-what-default precompile
	catch { file delete "$bname.h" }
	file copy "[file rootname $test].hs" "$bname.h"
	dg-test -keep-output "$bname.h" $flags ""

	# For the rest, the default is to compile to .s.
	set dg-do-what-default compile

	if { [ file exists "$bname.h.gch" ] } {
	    # Ensure that the PCH file is used, not the original header.
	    file delete "$bname.h"

	    dg-test -keep-output $test $flags "-I."
	    file delete "$bname.h.gch"
	    if { [ file exists "$bname.s" ] } {
		file rename "$bname.s" "$bname.s-gch"
		file copy "[file rootname $test].hs" "$bname.h"
		dg-test -keep-output $test $flags "-I."
		set tmp [ diff "$bname.s" "$bname.s-gch" ]
		if { $tmp == 0 } {
		    untested "$nshort $flags assembly comparison"
		} elseif { $tmp == 1 } {
		    pass "$nshort $flags assembly comparison"
		} else {
		    fail "$nshort $flags assembly comparison"
		}
		file delete "$bname.h"
		file delete "$bname.s"
		file delete "$bname.s-gch"
	    } else {
		untested "$nshort $flags assembly comparison"
	    }

	} else {
	    untested "$nshort $flags"
	    untested "$nshort $flags assembly comparison"
	}
    }
}

set dg-do-what-default "$old_dg_do_what_default"

# All done.
dg-finish
