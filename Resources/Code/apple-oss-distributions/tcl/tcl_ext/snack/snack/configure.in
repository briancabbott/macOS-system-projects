#!/bin/bash -norc
dnl	This file is an input file used by the GNU "autoconf" program to
dnl	generate the file "configure", which is run during Tcl installation
dnl	to configure the system for the local environment.
#
# RCS: @(#) $Id: configure.in,v 1.48 2008/11/05 00:13:00 hobbs Exp $

#-----------------------------------------------------------------------
# __CHANGE__
# Set your package name and version numbers here.
#
# This initializes the environment with PACKAGE_NAME and PACKAGE_VERSION
# set as provided.  These will also be added as -D defs in your Makefile
# so you can encode the package version directly into the source files.
#-----------------------------------------------------------------------

AC_INIT([snack], [2.2])

#--------------------------------------------------------------------
# Call TEA_INIT as the first TEA_ macro to set up initial vars.
# This will define a ${TEA_PLATFORM} variable == "unix" or "windows"
# as well as PKG_LIB_FILE and PKG_STUB_LIB_FILE.
#--------------------------------------------------------------------

TEA_INIT([3.9])

AC_CONFIG_AUX_DIR(tclconfig)

#--------------------------------------------------------------------
# Load the tclConfig.sh file
#--------------------------------------------------------------------

TEA_PATH_TCLCONFIG
TEA_LOAD_TCLCONFIG

#--------------------------------------------------------------------
# Load the tkConfig.sh file if necessary (Tk extension)
#--------------------------------------------------------------------

TEA_PATH_TKCONFIG
TEA_LOAD_TKCONFIG

#-----------------------------------------------------------------------
# Handle the --prefix=... option by defaulting to what Tcl gave.
# Must be called after TEA_LOAD_TCLCONFIG and before TEA_SETUP_COMPILER.
#-----------------------------------------------------------------------

TEA_PREFIX

#-----------------------------------------------------------------------
# Standard compiler checks.
# This sets up CC by using the CC env var, or looks for gcc otherwise.
# This also calls AC_PROG_CC, AC_PROG_INSTALL and a few others to create
# the basic setup necessary to compile executables.
#-----------------------------------------------------------------------

TEA_SETUP_COMPILER

#-----------------------------------------------------------------------
# __CHANGE__
# Specify the C source files to compile in TEA_ADD_SOURCES,
# public headers that need to be installed in TEA_ADD_HEADERS,
# stub library C source files to compile in TEA_ADD_STUB_SOURCES,
# and runtime Tcl library files in TEA_ADD_TCL_SOURCES.
# This defines PKG(_STUB)_SOURCES, PKG(_STUB)_OBJECTS, PKG_HEADERS
# and PKG_TCL_SOURCES.
#-----------------------------------------------------------------------

TEA_ADD_SOURCES([snack.c jkSound.c jkSoundEngine.c jkSoundEdit.c jkSoundFile.c
	jkCanvSpeg.c jkCanvWave.c jkCanvSect.c ffa.c g711.c
	jkFormatMP3.c jkSoundProc.c jkPitchCmd.c snackStubInit.c
	jkAudio.c jkMixer.c shape.c jkFilter.c jkSynthesis.c jkFilterIIR.c
	jkGetF0.c sigproc.c jkFormant.c sigproc2.c])
TEA_ADD_INCLUDES([-I\$(srcdir)/generic])
TEA_ADD_STUB_SOURCES([snackStubLib.c])
TEA_ADD_TCL_SOURCES([unix/snack.tcl])

AC_SUBST(CLEANFILES)

#--------------------------------------------------------------------
# __CHANGE__
# Choose which headers you need.  Extension authors should try very
# hard to only rely on the Tcl public header files.  Internal headers
# contain private data structures and are subject to change without
# notice.
# This MUST be called after TEA_LOAD_TCLCONFIG / TEA_LOAD_TKCONFIG
#--------------------------------------------------------------------

TEA_PUBLIC_TCL_HEADERS
#TEA_PRIVATE_TCL_HEADERS

TEA_PUBLIC_TK_HEADERS
#TEA_PRIVATE_TK_HEADERS
#TEA_PATH_X

#--------------------------------------------------------------------
# Check whether --enable-threads or --disable-threads was given.
# This auto-enables if Tcl was compiled threaded.
#--------------------------------------------------------------------

TEA_ENABLE_THREADS

#--------------------------------------------------------------------
# The statement below defines a collection of symbols related to
# building as a shared library instead of a static library.
#--------------------------------------------------------------------

TEA_ENABLE_SHARED

#--------------------------------------------------------------------
# This macro figures out what flags to use with the compiler/linker
# when building shared/static debug/optimized objects.  This information
# can be taken from the tclConfig.sh file, but this figures it all out.
#--------------------------------------------------------------------

TEA_CONFIG_CFLAGS

#--------------------------------------------------------------------
#	Set audio related variables for the supported systems
#--------------------------------------------------------------------

case $system in
    BSD/OS-4.*)
	TEA_ADD_CFLAGS([-DOSS])
	TEA_ADD_LIBS([-lossaudio])
	TEA_ADD_SOURCES([jkAudIO_oss.c])
        ;;
    HP-UX-*.1*)
	TEA_ADD_INCLUDES([-I/opt/audio/include])
	TEA_ADD_CFLAGS([-DHPUX])
	TEA_ADD_LIBS([-L/opt/audio/lib -lAlib])
	TEA_ADD_SOURCES([jkAudIO_hp.c])
	;;
    IRIX*)
	TEA_ADD_CFLAGS([-DIRIX])
        TEA_ADD_LIBS([-laudio])
	TEA_ADD_SOURCES([jkAudIO_sgi.c])
	;;
    IRIX64-6.*)
	TEA_ADD_CFLAGS([-DIRIX])
        TEA_ADD_LIBS([-laudio])
	TEA_ADD_SOURCES([jkAudIO_sgi.c])
	;;
    Linux*)
	AC_MSG_CHECKING([sound driver])
	AC_ARG_ENABLE(alsa,[  --enable-alsa           enable ALSA support],,enableval="no")
	if test "$enableval" != "no"; then
	    AC_MSG_RESULT(will use ALSA)
	    TEA_ADD_CFLAGS([-DALSA])
	    TEA_ADD_LIBS([-lasound])
	    TEA_ADD_SOURCES([jkAudIO_alsa.c])
	else
	    AC_MSG_RESULT(will use OSS)
	    TEA_ADD_CFLAGS([-DOSS])
	    TEA_ADD_SOURCES([jkAudIO_oss.c])
	fi
	;;
    NetBSD*|OpenBSD-*)
	TEA_ADD_CFLAGS([-DOSS])
	TEA_ADD_LIBS([-lossaudio])
	TEA_ADD_SOURCES([jkAudIO_oss.c])
        ;;
    FreeBSD-*)
	TEA_ADD_CFLAGS([-DOSS])
	TEA_ADD_SOURCES([jkAudIO_oss.c])
        ;;
    SunOS*)
	TEA_ADD_CFLAGS([-DSolaris])
	TEA_ADD_SOURCES([jkAudIO_sun.c])
	;;
    Darwin-*)
	TEA_ADD_CFLAGS([-DOS_X_CORE_AUDIO -DMAC_OSX_TCL -DMAC_OSX_TK])
	TEA_ADD_LIBS([-framework CoreAudio])
	TEA_ADD_SOURCES([jkAudIO_osx.c])
        ;;
    *)
	TEA_ADD_CFLAGS([-DOTHER])
	TEA_ADD_SOURCES([jkAudIO_skel.c])
	;;
esac

#--------------------------------------------------------------------
# Set the default compiler switches based on the --enable-symbols option.
#--------------------------------------------------------------------

TEA_ENABLE_SYMBOLS

#--------------------------------------------------------------------
# Everyone should be linking against the Tcl stub library.  If you
# can't for some reason, remove this definition.  If you aren't using
# stubs, you also need to modify the SHLIB_LD_LIBS setting below to
# link against the non-stubbed Tcl library.  Add Tk too if necessary.
#--------------------------------------------------------------------

AC_DEFINE(USE_TCL_STUBS, 1, [Use Tcl stubs])
AC_DEFINE(USE_TK_STUBS, 1, [Use Tk stubs])

#--------------------------------------------------------------------
# This macro generates a line to use when building a library.  It
# depends on values set by the TEA_ENABLE_SHARED, TEA_ENABLE_SYMBOLS,
# and TEA_LOAD_TCLCONFIG macros above.
#--------------------------------------------------------------------

TEA_MAKE_LIB

#--------------------------------------------------------------------
# Determine the name of the tclsh and/or wish executables in the
# Tcl and Tk build directories or the location they were installed
# into. These paths are used to support running test cases only,
# the Makefile should not be making use of these paths to generate
# a pkgIndex.tcl file or anything else at extension build time.
#--------------------------------------------------------------------

TEA_PROG_TCLSH
#TEA_PROG_WISH

#--------------------------------------------------------------------
# Finally, substitute all of the various values into the Makefile.
# You may alternatively have a special pkgIndex.tcl.in or other files
# which require substituting th AC variables in.  Include these here.
#--------------------------------------------------------------------

AC_OUTPUT([Makefile pkgIndex.tcl])
